#lang rhombus/static/and_meta

export:
  symbol_map_annot
  symbol_set_annot

defn.macro
| 'symbol_map_annot $(name :: Identifier) $(convert :: Identifier) $(unconvert :: Identifier):
     ~is_a $annot
     ...
     { $(src :: Identifier): $(dest :: Identifier), ...}':
    '«
       def table = { #'$src: #'$dest, ... }
       def rev_table = { #'$dest: #'$src, ... }
       fun is_symbol(v): table.has_key(v)
       fun $convert(v): table[v]
       fun $unconvert(v): rev_table[v]
       enum $name:
         ~is_a $annot
         ...
         $src
         ...
     »'
| 'symbol_map_annot $(name :: Identifier) $(convert :: Identifier):
     ~is_a $annot
     ...
     { $(src :: Identifier): $(dest :: Identifier), ...}':
    '«
       def table = { #'$src: #'$dest, ... }
       fun $convert(v): table[v]
       enum $name:
         ~is_a $annot
         ...
         $src
         ...
     »'

defn.macro 'symbol_set_annot $(name :: Identifier):
              ~is_a $annot
              ...
              { $(src :: Identifier), ...}':
  '«
     def table = { #'$src, ... }
     enum $name:
       ~is_a $annot
       ...
       $src
       ...
   »'
